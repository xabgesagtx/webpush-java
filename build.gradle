plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.1.1'

    id 'maven-publish'

}


group 'com.github.xabgesagtx'
version '0.1'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {

    // For cryptographic operations
    api group: 'org.bouncycastle', name: 'bcprov-jdk18on', version: '1.82'

    // For creating and signing JWT
    implementation group: 'org.bitbucket.b_c', name: 'jose4j', version: '0.9.6'

    // For parsing JSON
    testImplementation group: 'com.google.code.gson', name: 'gson', version: '2.8.9'

    // For testing, obviously
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.14.1'

    // For running JUnit tests
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.14.1'

    // For turning InputStream to String
    testImplementation group: 'commons-io', name: 'commons-io', version: '2.21.0'

    // For reading the demo vapid keypair from a pem file
    testImplementation group: 'org.bouncycastle', name: 'bcpkix-jdk18on', version: '1.81'

    // For verifying Base64Encoder results in unit tests
    testImplementation group: 'com.google.guava', name: 'guava', version: '33.4.8-jre'
}

wrapper {
    gradleVersion = '5.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
        showStandardStreams true
        exceptionFormat 'full'
    }

    exclude '**/SeleniumTests.class'
}

tasks.register('javadocJar', Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}


plugins {
    id 'java-library'
    id 'maven-publish'
}


group 'com.github.xabgesagtx'
version '0.1'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // For cryptographic operations
    api 'org.bouncycastle:bcprov-jdk18on:1.82'
    // For creating and signing JWT
    api 'org.bitbucket.b_c:jose4j:0.9.6'

    testImplementation group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.14.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    // For reading the demo vapid keypair from a pem file
    testImplementation 'org.bouncycastle:bcpkix-jdk18on:1.81'
    // For verifying Base64Encoder results in unit tests
    testImplementation 'com.google.guava:guava:33.4.8-jre'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
        showStandardStreams true
        exceptionFormat 'full'
    }

    exclude '**/SeleniumTests.class'
}

tasks.register('javadocJar', Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'webpush-java'
                description = 'A concise description of my library'
                url = 'https://github.com/xabgesagtx/webpush-java'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:xabgesagtx/webpush-java.git'
                    developerConnection = 'scm:git:git@github.com:xabgesagtx/webpush-java.git'
                    url = 'https://github.com/xabgesagtx/webpush-java'
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/xabgesagtx/webpush-java"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

